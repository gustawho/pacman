# Maintainer: Gustavo Castro < gustawho [at] gmail [dot] com >

pkgname=sushi4physics-2hdmc
_srcname=SusHi
pkgver=1.5.0
pkgrel=1
pkgdesc="Higgs production in the 2HDM (2HDMC support)"
url="http://sushi.hepforge.org/"
arch=('x86_64')
license=('GPL2')
depends=('lhapdf' '2hdmc')
makedepends=('gcc-fortran')
provides=('sushi4physics')
conflicts=('sushi4physics' 'sushi4physics-fh' 'sushi4physics-hb')
source=("http://www.hepforge.org/archive/sushi/${_srcname}-${pkgver}.tar.gz"
        "2hdmc.patch")
sha512sums=('55bc1c48bc856708f3cff1740abaee5cad8940a089173545719c0f26fbc836ace98a025335000396aaba9c193a8870e8dcaa25cc9b8e88f43c1d46f30d737553'
            '99d165b5625214955bb14794e7a39331b150ca75fe86bb1bfb040270323f45f0a1f1860b3be9d0a2a6143044291d1bb456cf52d8da572f7df82e41329b5b14da')

build() {
  patch "${srcdir}/${_srcname}-${pkgver}/Makefile" < "${srcdir}/2hdmc.patch"
  cd "${srcdir}/${_srcname}-${pkgver}"
  ./configure --prefix=/usr
  make predef=2HDMC
}

package() {
  mkdir -p "${pkgdir}/usr/bin" "${pkgdir}/usr/lib"
  cd "${srcdir}/${_srcname}-${pkgver}"
  install -m755 bin/sushi.2HDMC "${pkgdir}/usr/bin/sushi.2HDMC"
  install -m644 lib/libshare.a "${pkgdir}/usr/lib/libshare.a"
  install -m644 lib/libsushi2HDMC.a "${pkgdir}/usr/lib/libsushi2HDMC.a"
  ln -s /usr/bin/sushi.2HDMC "${pkgdir}/usr/bin/sushi"
  ln -s /usr/lib/libsushi2HDMC.a "${pkgdir}/usr/lib/libsushi.a"
}

# vim:set ts=2 sw=2 et:
