# Maintainer: Gustavo Castro < gustawho [at] gmail [dot] com >

pkgname=sushi4physics
_srcname=SusHi
pkgver=1.5.0
pkgrel=1
pkgdesc="Higgs production in the MSSM and the 2HDM - Standalone"
url="http://sushi.hepforge.org/"
arch=('x86_64')
license=('GPL2')
depends=('lhapdf')
makedepends=('gcc-fortran')
provides=('sushi4physics')
conflicts=('sushi4physics-2hdmc' 'sushi4physics-fh' 'sushi4physics-hb')
source=("http://www.hepforge.org/archive/sushi/${_srcname}-${pkgver}.tar.gz")
sha512sums=('55bc1c48bc856708f3cff1740abaee5cad8940a089173545719c0f26fbc836ace98a025335000396aaba9c193a8870e8dcaa25cc9b8e88f43c1d46f30d737553')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  mkdir -p "${pkgdir}/usr/bin" "${pkgdir}/usr/lib"
  cd "${srcdir}/${_srcname}-${pkgver}"
  install -m755 bin/sushi.PLAIN "${pkgdir}/usr/bin/sushi.PLAIN"
  install -m644 lib/libshare.a "${pkgdir}/usr/lib/libshare.a"
  install -m644 lib/libsushiPLAIN.a "${pkgdir}/usr/lib/libsushiPLAIN.a"
  ln -s /usr/bin/sushi.PLAIN "${pkgdir}/usr/bin/sushi"
  ln -s /usr/lib/libsushiPLAIN.a "${pkgdir}/usr/lib/libsushi.a"
}

# vim:set ts=2 sw=2 et:
